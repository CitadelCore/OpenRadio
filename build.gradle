buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle.forge'

version = "0.9.6-MC1.12.2"
group = "at.chaosfield.openradio"
archivesBaseName = "OpenRadio"

if (hasProperty('buildnumber')) {
    version = "${version}-${this.properties['buildnumber']}"
}

minecraft {
    version = "1.12.2-14.23.4.2759"
    runDir = "run"

    mappings = "snapshot_20171003"
    //mappings = "stable_20"
}

repositories {
    maven {
        name = "mightypirates"
        url = "http://maven.cil.li/"
    }
    maven {
        name = "ActuallyAdditions"
        url = "http://maven.chaosfield.at/"
    }
}

dependencies {
    compile files("lib/appliedenergistics2-rv5-stable-11-api.jar")
    provided "li.cil.oc:OpenComputers:MC1.12.2-1.7.+"
    provided "de.ellpeck.actuallyadditions:ActuallyAdditions:1.12.2-r140"
}

processResources
        {
            // this will ensure that this task is redone when the versions change.
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            // replace stuff in mcmod.info, nothing else
            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'

                // replace version and mcversion
                expand 'version': project.version, 'mcversion': project.minecraft.version
            }

            // copy everything else, thats not the mcmod.info
            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }

javadoc {
    include 'at/chaosfield/openradio/api/**'
}

task apiJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.java
    classifier = 'api'
    include 'at/chaosfield/openradio/api/**'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc'
    classifier 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java
    classifier = 'sources'
}

artifacts {
    //archives apiJar
    //archives javadocJar
    archives sourcesJar
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            //artifact apiJar
            //artifact javadocJar
            artifact sourcesJar
        }
    }
    repositories {
        maven {
            url "file:///srv/nginx/maven"
        }
    }
}

idea { module { inheritOutputDirs = true } }
